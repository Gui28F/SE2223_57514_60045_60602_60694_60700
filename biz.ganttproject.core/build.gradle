// import com.sun.org.apache.xalan.internal.xsltc.compiler.Copy

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "maven-publish"
}
configurations {
    direct
}

dependencies {
    providedCompile project(path: ':biz.ganttproject.app.libs', configuration: 'exported')
    direct fileTree(dir: project.ext.libDir, include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.+'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.6.+'
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    api "com.google.guava:guava:31.+"

    direct configurations.api
    implementation configurations.direct
    implementation configurations.providedCompile
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.4'


    testImplementation(
        'org.junit.jupiter:junit-jupiter-api:5.+',
        'org.mockito:mockito-core:4.8.0',
        'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    )
    testRuntimeOnly(
        'org.junit.jupiter:junit-jupiter-engine:5.+'
    )
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

test {
    useJUnitPlatform()
}

task copyPlugin(dependsOn: assemble) {
    doFirst {
        println "Copying $project.name to $rootProject.pluginsDir"
    }
    doLast {
        println "Installing $project.name"
        copy {
            into(new File(rootProject.pluginsDir, project.name))
            from(fileTree(".")) {
                include "plugin.xml"
            }
            from(fileTree("src/main/")) {
                include "resources/**"
            }
        }
        copy {
            into(new File(rootProject.pluginsDir, "${project.name}/lib/"))
            from(configurations.direct) {
                include "*.jar"
            }
            from(jar.outputs.getFiles().getFiles().flatten())
            rename { filename -> filename + ".lib" }
        }
        println "<<< $project.name"
    }

}

addPublishing(project)
publishing {
    publications {
        core(MavenPublication) {
            artifactId = 'biz.ganttproject.core'
            artifact jar
        }
    }
}
